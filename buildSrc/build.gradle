
apply plugin: 'java'

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'codenarc'
    apply plugin: 'cobertura'

    tasks.withType(CodeNarc) { codeNarcTask ->
        codeNarcTask.ignoreFailures = true
        codeNarcTask.configFile = file("../../config/codenarc/codenarc.xml")
        codeNarcTask.maxPriority1Violations = 2
        codeNarcTask.maxPriority2Violations = 4
        codeNarcTask.maxPriority3Violations = 7
    }
    tasks.getByName('check').dependsOn('cobertura')

    sourceSets.main.java.srcDirs = []
    sourceSets.main.groovy.srcDirs += ['src/main/java']
}

repositories {
    mavenCentral()
}
dependencies {

    compile localGroovy()
    compile 'commons-io:commons-io:2.4'
    compile 'com.dropbox.core:dropbox-core-sdk:1.7.6'
    compile 'joda-time:joda-time:2.3'
    compile 'net.sourceforge.nekohtml:nekohtml:1.9.19'

    compile 'org.jsoup:jsoup:1.7.3'
    compile 'com.rockymadden.stringmetric:stringmetric-core_2.10:0.27.3'

    testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
        exclude group: 'org.codehaus.groovy'
    }


    testCompile 'junit:junit:4.11'

    //Add  all subprojects as runtime dependencies to the root buildSrc project.
    runtime subprojects.collect { owner.project(it.path)}

    runtime('com.uwyn:jhighlight:1.0') {
        exclude module: "servlet-api"
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:2.2.0'
    }
}

task groovydoc(type: Groovydoc,overwrite: true) {
    docTitle "FMI Help System Documentation"
    windowTitle "FMI Help System Documentation"
    destinationDir file("build/groovydoc/")
    source subprojects.groovydoc.source
    classpath = files(subprojects.groovydoc.classpath)
    groovyClasspath = project(":plugins").groovydoc.groovyClasspath
}
